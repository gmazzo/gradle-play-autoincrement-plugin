apply plugin: 'groovy'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def repoName = 'gradle-play-autoincrement-plugin'
def repoDesc = 'An Android-Gradle plugin to set the build version based on the last APK uploaded on PlayStore'
def repoUrl = "https://github.com/gmazzo/$repoName"
def repoTags = ['android', 'gradle', 'version', 'apk', 'versionCode', 'autoincrement']

dependencies {
    compile gradleApi()

    testCompile 'junit:junit:4.12'
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = repoName
        desc = repoDesc
        vcsUrl = "${repoUrl}.git"
        githubRepo = repoUrl.replaceFirst('\\Qhttps://github.com/\\E', '');
        githubReleaseNotesFile = 'README.md'
        licenses = ['Public Domain']
        labels = repoTags
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name repoDesc
                url repoUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
