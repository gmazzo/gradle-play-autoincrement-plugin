plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven'
    id 'com.jfrog.bintray' version '1.7.3'
}

def repoName = 'gradle-play-autoincrement-plugin'
def repoDesc = 'An Android-Gradle plugin to set the build version based on the last APK uploaded on PlayStore'
def repoUrl = "https://github.com/gmazzo/$repoName"
def repoTags = ['android', 'gradle', 'version', 'apk', 'versionCode', 'autoincrement']

dependencies {
    compile 'com.android.tools.build:gradle:2.2.3'
    compile 'com.google.apis:google-api-services-androidpublisher:v2-rev41-1.22.0'

    testCompile 'junit:junit:4.12'
}

gradlePlugin {
    plugins {
        autoincrement {
            id = 'com.github.gmazzo.play-autoincrement'
            implementationClass = 'com.github.gmazzo.gradle.plugins.PlayAutoincrementPlugin'
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = repoName
        desc = repoDesc
        vcsUrl = "${repoUrl}.git"
        githubRepo = repoUrl.replaceFirst('\\Qhttps://github.com/\\E', '');
        githubReleaseNotesFile = 'README.md'
        licenses = ['Public Domain']
        labels = repoTags
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                name repoDesc
                url repoUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// for functional testing
test {
    dependsOn install
    doFirst {
        systemProperty 'PLUGIN_DEPENDENCY', "${project.group}:play-autoincrement-plugin:${project.version}"
    }
}
