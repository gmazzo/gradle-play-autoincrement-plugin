plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

group = 'gmazzo'
version = '0.5'

def pluginId = 'com.github.gmazzo.play-autoincrement'
def repoName = 'gradle-play-autoincrement-plugin'
def repoDesc = 'An Android-Gradle plugin to set the build version based on the last APK uploaded on PlayStore'
def repoUrl = "https://github.com/gmazzo/$repoName"
def repoTags = ['android', 'gradle', 'version', 'apk', 'versionCode', 'autoincrement']

dependencies {
    compile 'com.android.tools.build:gradle:2.2.3'
    compile 'com.google.apis:google-api-services-androidpublisher:v2-rev41-1.22.0'

    testCompile 'junit:junit:4.12'
}

// for functional testing
test {
    dependsOn install
    doFirst {
        systemProperty 'PLUGIN_DEPENDENCY', "gradle.plugin.${project.group}:play-autoincrement-plugin:${project.version}"
    }
    testLogging.showStandardStreams = true
}

install {
    repositories.mavenInstaller {
        pom.groupId = "gradle.plugin.${project.group}"
    }
}

gradlePlugin {
    plugins {
        autoincrement {
            id = pluginId
            implementationClass = 'com.github.gmazzo.gradle.plugins.PlayAutoincrementPlugin'
        }
    }
}

pluginBundle {
    website = repoUrl
    vcsUrl = repoUrl
    description = repoDesc
    tags = repoTags

    plugins {
        autoincrement {
            id = pluginId
            displayName = 'Gradle PlayStore Autoincrement Plugin'
        }
    }
}
